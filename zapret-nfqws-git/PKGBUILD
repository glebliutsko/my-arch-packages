_pkgname=zapret-nfqws
pkgname=${_pkgname}-git
pkgver=r227.8bdb5dc
pkgrel=1
pkgdesc='Bypass deep packet inspection.'
arch=('any')
url='https://github.com/bol-van/zapret'
license=('GPL')
depends=('systemd' 'curl')
makedepends=('libnetfilter_queue' 'git')

source=("$_pkgname::git+https://github.com/bol-van/zapret.git"
        'zapret-nfqws-update-blocklist.sh'
        'zapret-nfqws.service'
        'zapret-nfqws-update-blocklist.service'
        'zapret-nfqws-update-blocklist.timer'
        'zapret-user-hosts.txt'
        'config')
sha256sums=('SKIP'
            '413753e7cd5a2c08ae113648cdaca594bfbc5dc17cfc8c112c46b5b6cb331536'
            'f85f3b4c13c7985d4b883da26e68864cb059da0d5af94d3f460aa0fb927ee999'
            '3efaf5728bf8f129748738a7a2a1635094a1676cae58553b3614f58681df8d8b'
            '9c8872fc11d4ba434265ee77ddf58e4bfb682f28f77293ff84607590d6c496c7'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            '3a89773bcea26b4d062a0c1480c940b3bcd39789b3e9534f2a194b0f3d84c9da')

install=zapret-nfqws.install

backup=('etc/zapret-nfqws/zapret-user-hosts.txt'
        'etc/zapret-nfqws/config')

pkgver() {
    cd "$_pkgname"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
    cd "$_pkgname"
    make
}

package() {
    mkdir -p -m 755 "$pkgdir/var/lib/zapret-nfqws"
    
    install -Dm 644 "$srcdir/zapret-nfqws.service" "$pkgdir/usr/lib/systemd/system/zapret-nfqws.service"
    install -Dm 644 "$srcdir/zapret-nfqws-update-blocklist.service" "$pkgdir/usr/lib/systemd/system/zapret-nfqws-update-blocklist.service"
    install -Dm 644 "$srcdir/zapret-nfqws-update-blocklist.timer" "$pkgdir/usr/lib/systemd/system/zapret-nfqws-update-blocklist.timer"

    install -Dm 644 "$srcdir/zapret-user-hosts.txt" "$pkgdir/etc/zapret-nfqws/zapret-user-hosts.txt"
    install -Dm 644 "$srcdir/config" "$pkgdir/etc/zapret-nfqws/config"

    install -Dm 755 "$srcdir/$_pkgname/binaries/my/nfqws" "$pkgdir/usr/bin/zapret-nfqws"
    install -Dm 755 "$srcdir/zapret-nfqws-update-blocklist.sh" "$pkgdir/usr/lib/zapret-nfqws-update-blocklist"
}
